ext {
    reportDirectory = "$project.buildDir/reports"
}

test {
    reports {
        junitXml.enabled = false
        html.enabled = true
    }
}

pmd {
    toolVersion = '5.6.1'
    sourceSets = [sourceSets.main]
    ruleSets = [
            'java-basic',
            'java-braces',
            'java-clone',
            'java-empty',
            'java-finalizers',
            'java-strictexception',
            'java-strings',
            'java-sunsecure',
            'java-typeresolution',
            'java-unnecessary'
    ]
}

tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

findbugs {
    toolVersion = '3.0.1'
    sourceSets = [sourceSets.main]
    effort = 'max'
    reportLevel = 'high'
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

checkstyle {
    toolVersion = '7.6.1'
    sourceSets = [sourceSets.main]
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

codenarc {
    toolVersion = '0.27.0'
    sourceSets = [sourceSets.test]
    configFile = rootProject.file 'config/codenarc/codenarc.groovy'
}

jacoco {
    toolVersion = '0.7.9'
}

jacocoTestReport {
    reports {
        xml.enabled = false
        csv.enabled = false
        html.enabled = true
    }
}

pitest {
    targetClasses = ['org.home.game.*']
    threads = 4
    outputFormats = ['HTML']
}

gradleLint {
    criticalRules = [
            'dependency-parentheses',
            'overridden-dependency-version',
            'unused-exclude-by-dep',
            'archaic-wrapper',
            'all-nebula-renames'
    ]
}